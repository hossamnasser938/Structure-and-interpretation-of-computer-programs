# 2.4 Multiple representations for Abstract Data

- We have been talking about **data abstraction** and its importance for building **maintainable systems**. **Data Abstraction** allowed us to separate the use of data objects from its underlying representation.

- This is a really powerful mechanism. But we can go beyond this. What if we have multiple representations for data objects and both of them is appropriate over the other in a defined context. For example complex numbers can be represented in 2 ways:

  - **Rectangular form** in which a complex number is defined in terms of a **real part** and an **imaginary part**.
  - **Polar form** in which a complex number is defined in terms of a **magnitude** and an **angle**.

- We need a way to build a system that does not only errect **horizontal bariers** that we have been dealing with that separate higher-level procedures from lower-level ones, but also errect vertical bariers that allow multiple representations of data objects to coexist in the system. For this to happen, we have to build generic selectors and constructors that know how to deal with those different types of objects.

```
          Programs that use complex numbers
  +-------------------------------------------------+
--| add-complex sub-complex mul-complex div-complex |--
  +-------------------------------------------------+
              Complex arithmetic package
---------------------------+---------------------------
          Rectangular      |         Polar
        representation     |     representation
---------------------------+---------------------------
    List structure and primitive machine arithmetic
```

- We will discuss a mechanism for doing that named **type tags** which attaches a type tag to a data object to identify its type and build generic procedures that use this type tags to know how to deal with this type of data.

- What's really important as well is to maintain this in an additive style which means that we need to be flexible adding new representations to the system without the need to refactor anything. We will do this by introducing a powerful mechanism named **data-directed programming**.

## 2.4.1 Representations for Complex Numbers

- We're going to build a system that performs arithmetic operations on complex numbers.

- A complex number can be defined in 2 forms:

  - **Rectangular form** with a real part and an imaginary part

  ```
  z = x + iy , where x is the real part, and y is the imaginary part
  ```

  - **Polar form** with a magnitude and an angle

  ```
  z = r * e^iΘ , where r is the magnitude and Θ is the angle
  ```

- To sum or subtract complex numbers, it makes sense to think of the rectangular form

```
z = z1 + z2
(real-part z) = (real-part z1) + (real-part z2)
(imag-part z) = (imag-part z1) + (imag-part z2)
```

```
z = z1 - z2
(real-part z) = (real-part z1) - (real-part z2)
(imag-part z) = (imag-part z1) - (imag-part z2)
```

- To multiply or divide complex numbers, it makes sense to think of the polar form:

```
z = z1 * z2
(magnitude z) = (magnitude z1) * (magnitude z2)
(angle z) = (angle z1) + (angle z2)
```

```
z = z1 / z2
(magnitude z) = (magnitude z1) / (magnitude z2)
(angle z) = (angle z1) - (angle z2)
```

- From data abstraction point-of-view, we can defer the decision of which representation to be used and assume that those procedures exist `real-part`, `imag-part`, `magnitude`, `angle`, `make-from-real-imag`, and `make-from-mag-ang`. Now we can go and build the procedures that perform arithmetic operations on complex numbers in terms of those procedures:

```
(define (add-complex z1 z2)
  (make-from-real-imag
    (+ (real-part z1) (real-part z2))
    (+ (imag-part z1) (imag-part z2))))
```

```
(define (sub-complex z1 z2)
  (make-from-real-imag
    (- (real-part z1) (real-part z2))
    (- (imag-part z1) (imag-part z2))))
```

```
(define (mul-complex z1 z2)
  (make-from-mag-ang
    (* (magnitude z1) (magnitude z2))
    (+ (angle z1) (angle z2))))
```

```
(define (div-complex z1 z2)
  (make-from-mag-ang
    (/ (magnitude z1) (magnitude z2))
    (- (angle z1) (angle z2))))
```

- Now if we decided to represent complex numbers in the rectangular form, we will end up with those procedures:

```
(define (real-part z) (car z))

(define (imag-part z) (cdr z))

(define (magnitude z)
  (sqrt (+ (square (real-part z)) (square (imag-part z)))))

(define (angle z)
  (atan (imag-part z) (real-part z)))

(define (make-from-real-imag x y) (cons x y))

(define (make-from-mag-ang r a)
  (cons (* r (cos a)) (* r (sin a))))
```

- And if we decided to represent complex numbers in the polar form, we will end up with those procedures:

```
(define (real-part z)
  (* (magnitude z) (cos (angle z))))

(define (imag-part z)
  (* (magnitude z) (sin (angle z))))

(define (magnitude z) (car z))

(define (angle z) (cdr z))

(define (make-from-real-imag x y)
  (cons (sqrt (+ (square x) (square y))) (atan y x)))

(define (make-from-mag-ang r a) (cons r a))
```

## 2.4.2 Tagged Data

- Now we need to go further and permit the existence of the 2 representation in the system. In order to do that we will use a pwerful mechanism named **Tagged Data**.

- When we permit the existence of the 2 representation we need a way to distinguish between them cuz if you've been given a pair `(3, 4)`, without identifying the type, you cannot decide whether the `magnitude` is `5` if it is in **rectangular** form or `3` if it is in **polar** form.

- To deal with this issue, we will attach a type tag(the symbol "rectangular" or "polar") with the data object which we can strip off at any moment to identify the data object type:

```
(define (attach-tag type-tag contents)
  (cons type-tag contents))

(define (type-tag datum)
  (car datum))

(define (contents datum)
  (cdr datum))
```

- Now we can build predicates to check the data object type:

```
(define (rectangular? complex)
  (eq? (type-tag complex) 'rectangular))

(define (polar? complex)
  (eq? (type-tag complex) 'polar))
```

- Now In order to adopt both representations in the system, we need to :

  - refactor the `make` procedure of each to attach the tag.
  - rename procedures to not conflict with the other representation procedures by adding a suffix.

- Rectangular form procedures:

```
(define (real-part-rectangular z) (car z))

(define (imag-part-rectangular z) (cdr z))

(define (magnitude-rectangular z)
  (sqrt (+ (square (real-part-rectangular z)) (square (imag-part-rectangular z)))))

(define (angle-rectangular z)
  (atan (imag-part-rectangular z) (real-part-rectangular z)))

(define (make-from-real-imag-rectangular x y)
  (attach-tag 'rectangular (cons x y)))

(define (make-from-mag-ang-rectangular r a)
  (attach-tag 'rectangular (cons (* r (cos a)) (* r (sin a)))))
```

- Polar form procedure:

```
(define (real-part-polar z)
  (* (magnitude-polar z) (cos (angle-polar z))))

(define (imag-part-polar z)
  (* (magnitude-polar z) (sin (angle-polar z))))

(define (magnitude-polar z) (car z))

(define (angle-polar z) (cdr z))

(define (make-from-real-imag-polar x y)
  (attach-tag 'polar (cons (sqrt (+ (square x) (square y))) (atan y x))))

(define (make-from-mag-ang-polar r a)
  (attach-tag 'polar (cons r a)))
```

- Last but not least, we can build the generic selectors to use the type tag to get the data:

```
(define (real-part complex)
  (cond ((rectangular? complex) (real-part-rectangular (contents complex))
        ((polar? complex) (real-part-polar (contents complex)))))

(define (imag-part complex)
  (cond ((rectangular? complex) (imag-part-rectangular (contents complex))
        ((polar? complex) (imag-part-polar (contents complex)))))

(define (magnitude complex)
  (cond ((rectangular? complex) (magnitude-rectangular (contents complex))
        ((polar? complex) (magnitude-polar (contents complex)))))

(define (angle complex)
  (cond ((rectangular? complex) (angle-rectangular (contents complex))
        ((polar? complex) (angle-polar (contents complex)))))
```

- Finally, we have to decide the representation to be used to construct data objects. One reasonable choice is to use rectangular representation when we have real and imaginary parts and to use polar representation when we have magnitude and angle:

```
(define (make-from-real-imag real image)
  (make-from-real-imag-rectangular real image))

(define (make-from-mag-ang magnitude angle)
  (make-from-mag-ang-polar magnitude angle))
```

- Now we can use the predefined arithmetic procedures `add-complex`, `sub-complex`, `mul-complex`, `div-complex` without any change.

## 2.4.3 Data-Directed Programming and Additivity

- We managed to adopt multiple representations for complex numbers in our systems using **dispatch on type** approach. However, we can identify 2 problems:

  - We needed to rename the procedures differently in order to prevent naming conflicts
  - In order to adopt new representations, we will have to refactor all generic selectors to add a conditional for this new representation.

- The general problem here is that our system design is not additive. To deal with this issue, we can use a mechanism named **Data-Directed programming** in which we replace the conditioning in the generic selectors using a 2 dimentional table that has 2 axis one for the operation type and the other is for the data object type and they intersect with the procedure to be used for this operation type on this data object type.

```
           |               Types
Operations | Polar           | Rectangular
===========+=================+======================
real-part  | real-part-polar | real-part-rectangular
imag-part  | imag-part-polar | imag-part-rectangular
magnitude  | magnitude-polar | magnitude-rectangular
angle      | angle-polar     | angle-rectangular
```

- We will assume that we have 2 procedures to maintain this table:

  - `put <op> <type> <proc>` that inserts a procedure `proc` for the operation `op` and the data object type `type`
  - `get <op> <type>` that gets the procedure that performs the operation `op` on the data object type `type`

- We can install the rectangular package through this procedure:

```
(define (install-rectangular-package)
;; internal procedures
(define (real-part z) (car z))
(define (imag-part z) (cdr z))
(define (make-from-real-imag x y) (cons x y))
(define (magnitude z)
  (sqrt (+ (square (real-part z))
          (square (imag-part z)))))
(define (angle z)
  (atan (imag-part z) (real-part z)))
(define (make-from-mag-ang r a)
  (cons (* r (cos a)) (* r (sin a))))

;; interface to the rest of the system
(define (tag x) (attach-tag ’rectangular x))
(put ’real-part ’(rectangular) real-part)
(put ’imag-part ’(rectangular) imag-part)
(put ’magnitude ’(rectangular) magnitude)
(put ’angle ’(rectangular) angle)
(put ’make-from-real-imag ’rectangular
    (lambda (x y) (tag (make-from-real-imag x y))))
(put ’make-from-mag-ang ’rectangular
    (lambda (r a) (tag (make-from-mag-ang r a))))
’done)
```

- We can also install the polar package through this procedure:

```
(define (install-polar-package)
;; internal procedures
(define (magnitude z) (car z))
(define (angle z) (cdr z))
(define (make-from-mag-ang r a) (cons r a))
(define (real-part z)
  (* (magnitude z) (cos (angle z))))
(define (imag-part z)
  (* (magnitude z) (sin (angle z))))
(define (make-from-real-imag x y)
  (cons (sqrt (+ (square x) (square y)))
        (atan y x)))

;; interface to the rest of the system
(define (tag x) (attach-tag ’polar x))
(put ’real-part ’(polar) real-part)
(put ’imag-part ’(polar) imag-part)
(put ’magnitude ’(polar) magnitude)
(put ’angle ’(polar) angle)
(put ’make-from-real-imag ’polar
    (lambda (x y) (tag (make-from-real-imag x y))))
(put ’make-from-mag-ang ’polar
    (lambda (r a) (tag (make-from-mag-ang r a))))
’done)
```

- Note that we did not need to change procedure names since they are internal to the procedures `install-rectangular-package` and `install-polar-package`

- Now we will build the procedure `apply-generic` that will an operation on an object:

```
(define (apply-generic op object)
  (let ((proc (get op (type-tag object))))
       (if proc
           (proc (contents object))
           (error "no proc"))))
```

- Now we can define the generic selectors to just call `apply-generic` procedure:

```
(define (real-part complex)
  (apply-generic 'real-part complex))

(define (imag-part complex)
  (apply-generic 'imag-part complex))

(define (magnitude complex)
  (apply-generic 'magnitude complex))

(define (angle complex)
  (apply-generic 'angle complex))
```

- We can also extract from the table the constructor that fits based on the available args as we did earlier:

```
(define (make-from-real-imag x y)
  ((get ’make-from-real-imag ’rectangular) x y))
(define (make-from-mag-ang r a)
  ((get ’make-from-mag-ang ’polar) r a))
```

- By this, in order to add new representation, all we have to do is to install a new package without refactoring anything.

### Message Passing

- In order to adopt multiple representations for complex numbers in the system, we introduced 2 approaches:

  - **dispatch on type** in which each generic operation handles a row from the table
  - **data-directed programming** in whcih we represet the table itself and build `apply-generic` procedure to find the suitable operation based on the type of object from the table and apply it

- For the first approach, we can present it as intilligent operations that dispatch on data objects. We can do things differently. We can build operations that dispatch on intilligent data objects by building the `make` procedures to return a dispacth procedure that given an operation name, it handles it:

```
(define (make-from-real-imag x y)
  (define (dispatch op)
    (cond ((eq? op ’real-part) x)
          ((eq? op ’imag-part) y)
          ((eq? op ’magnitude)
          (sqrt (+ (square x) (square y))))
          ((eq? op ’angle) (atan y x))
          (else
          (error "Unknown op -- MAKE-FROM-REAL-IMAG" op))))
  dispatch)
```

and then we can define `apply-generic` just to forward the operation to the data object which is the `dispatch` procedure:

```
(define (apply-generic op arg) (arg op))
```

- This approach is known as **message passing** and we used it before as alternative representation for `cons` using only procedures. Know we present it as a valuable tool for managing multiple representation in the system.

#### Exercise 2.75

```
(define (make-from-mag-ang mag ang)
  (define (dispatch op)
    (cond ((eq? op 'magnitude) mag)
          ((eq? op 'angle) ang)
          ((eq? op 'real-part) (* mag (cos ang))
          ((eq? op 'imag-part) (* mag (sin ang))
          (else (error "error op")))))
  dispatch)
```
